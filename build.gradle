plugins {
    id 'java'
    id 'org.springframework.boot' version "${springBootVersion}"
    id 'io.spring.dependency-management' version "${springBootDMVersion}"
    id 'org.openapi.generator' version "${openApiGeneratorVersion}"
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    //spring
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
//    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml'


    // db
    implementation 'org.flywaydb:flyway-core'
    runtimeOnly 'org.postgresql:postgresql'

    // api
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:2.1.0"
    implementation "org.openapitools:jackson-databind-nullable:${jacksonDatabindNullableVersion}"
    implementation "javax.annotation:javax.annotation-api:${javaxAnnotationApi}"
    implementation "io.swagger:swagger-annotations:${swaggerAnnotations}"
    implementation "com.google.code.findbugs:jsr305:${jsr305Version}"



    // annotations
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    implementation "org.mapstruct:mapstruct:${mapstructVersion}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
    implementation "org.projectlombok:lombok-mapstruct-binding:${lombomMapstructBindingVersion}"

    // test
    testImplementation "io.rest-assured:rest-assured:${restAssuredVersion}"
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:postgresql'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
//    testImplementation 'org.springframework.security:spring-security-test'
}

//                                          OPEN API
openApiGenerate {
    generatorName.set("java")
    library.set("webclient")
    inputSpec.set("$rootDir/src/main/resources/petstore-v3.0.2.json")
    outputDir.set("$buildDir/generated-sources/openapi")
    apiPackage.set("com.example.infrastructure.petstore.api")
    modelPackage.set("com.example.infrastructure.petstore.model")
    configOptions.set([
            serializableModel: "true",
            dateLibrary: "java8",
            serializationLibrary: "jackson"
    ])
}

sourceSets {
    main {
        java.srcDirs += "$buildDir/generated-sources/openapi/src/main/java"
    }
}

compileJava.dependsOn tasks.named("openApiGenerate")

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

tasks.named('test') {
    useJUnitPlatform()
}
